plugins {
}

apply plugin: 'java'
apply plugin: 'maven-publish'

group = 'org.intellij.plugins.hcl.terraform'
version = '2023.2.1'

boolean isSnapshot = version.endsWith('-SNAPSHOT')

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

processResources {
    doLast {
        if (!isSnapshot) {
            print "${outputs.files.asPath}"
            fileTree(dir: outputs.files.asPath, include: '**/*.json').each {
                file -> file.text = groovy.json.JsonOutput.toJson(new groovy.json.JsonSlurper().parse(file))
            }
        }
    }
}

sourceSets.main.resources { srcDir 'terraform' }

jar {
    includeEmptyDirs false
    eachFile { details ->
        details.path = "terraform/$details.path"
    }
}

def spaceUsername = rootProject.getProperty('spaceUsername')
def spacePassword = rootProject.getProperty('spacePassword')

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version
            suppressAllPomMetadataWarnings()
        }
    }
    repositories {
        maven {
            url "https://packages.jetbrains.team/maven/p/ij/intellij-dependencies"
            credentials {
                username = spaceUsername
                password = spacePassword
            }
        }
    }
}

