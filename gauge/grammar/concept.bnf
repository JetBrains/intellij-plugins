/*
 * Copyright (C) 2020 ThoughtWorks, Inc.
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

{
  parserClass="com.thoughtworks.gauge.parser.ConceptParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Concept"
  psiImplClassSuffix="Impl"
  psiPackage="com.thoughtworks.gauge.language.psi"
  psiImplPackage="com.thoughtworks.gauge.language.psi.impl"

  elementTypeHolderClass="com.thoughtworks.gauge.language.token.ConceptTokenTypes"
  elementTypeClass="com.thoughtworks.gauge.language.token.ConceptElementType"
  tokenTypeClass="com.thoughtworks.gauge.language.token.ConceptTokenType"

  psiImplUtilClass="com.thoughtworks.gauge.language.psi.ConceptPsiImplUtil"
}

conceptFile ::= (comment)* concept+
concept ::= conceptHeading NEW_LINE* (step NEW_LINE* | comment) +
private comment ::= COMMENT
private conceptComment ::= CONCEPT_COMMENT
conceptHeading ::= ((CONCEPT_HEADING_IDENTIFIER (CONCEPT_HEADING|dynamicArg)* NEW_LINE) | (CONCEPT_HEADING))
step ::= STEP_IDENTIFIER (arg|STEP)+ (comment | NEW_LINE)* table?    {mixin="com.thoughtworks.gauge.language.psi.impl.ConceptNamedElementImpl"
                                   implements="com.thoughtworks.gauge.language.psi.ConceptNamedElement" methods = [getStepValue]}
arg ::= dynamicArg | staticArg
dynamicArg ::= DYNAMIC_ARG_START DYNAMIC_ARG DYNAMIC_ARG_END
staticArg ::= ARG_START ARG? ARG_END
table ::= tableHeader tableBody
tableHeader ::= TABLE_BORDER (TABLE_HEADER TABLE_BORDER)+ NEW_LINE ((TABLE_BORDER)* NEW_LINE)?
tableBody ::=(TABLE_BORDER (WHITESPACE* tableRowValue? WHITESPACE* TABLE_BORDER)+ NEW_LINE?)*
tableRowValue ::= TABLE_ROW_VALUE+ | (DYNAMIC_ARG_START DYNAMIC_ARG+ DYNAMIC_ARG_END WHITESPACE* TABLE_ROW_VALUE* WHITESPACE*)+
