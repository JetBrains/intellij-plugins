{
  psiImplUtilClass="com.jetbrains.lang.dart.util.DartPsiImplUtil"

  parserClass="com.jetbrains.lang.dart.DartParser"
  parserUtilClass="com.jetbrains.lang.dart.DartGeneratedParserUtilBase"

  implements="com.jetbrains.lang.dart.psi.DartPsiCompositeElement"
  extends="com.jetbrains.lang.dart.psi.impl.DartPsiCompositeElementImpl"

  psiClassPrefix="Dart"
  psiImplClassSuffix="Impl"
  psiPackage="com.jetbrains.lang.dart.psi"
  psiImplPackage="com.jetbrains.lang.dart.psi.impl"

  elementTypeHolderClass="com.jetbrains.lang.dart.DartTokenTypes"
  elementTypePrefix=""
  elementTypeClass="com.jetbrains.lang.dart.DartElementType"
  tokenTypeClass="com.jetbrains.lang.dart.DartElementType"

  tokenTypeClass="com.jetbrains.lang.dart.DartTokenTypes"

  generateFirstCheck=50

  tokens = [
// reserved words
  ASSERT="assert"
  BREAK="break"
  CASE="case"
  CATCH="catch"
  CLASS="class"
  CONST="const"
  CONTINUE="continue"
  DEFAULT="default"
  DO="do"
  ELSE="else"
  ENUM="enum"
  EXTENDS="extends"
  FALSE="false"
  FINAL="final"
  FINALLY="finally"
  FOR="for"
  IF="if"
  IN="in"
  IS="is"
  NEW="new"
  NULL="null"
  RETHROW="rethrow"
  RETURN="return"
  SUPER="super"
  SWITCH="switch"
  THIS="this"
  THROW="throw"
  TRUE="true"
  TRY="try"
  VAR="var"
  WHILE="while"
  WITH="with"

// void
  VOID="void"

// BUILT_IN_IDENTIFIER
  ABSTRACT="abstract"
  AS="as"
  DEFERRED="deferred"
  EXPORT="export"
  EXTERNAL="external"
  FACTORY="factory"
  GET="get"
  IMPLEMENTS="implements"
  IMPORT="import"
  LIBRARY="library"
  OPERATOR="operator"
  PART="part"
  SET="set"
  STATIC="static"
  TYPEDEF="typedef"

// other
  ON="on"
  OF="of"
  NATIVE="native"
  SHOW="show"
  HIDE="hide"
  SYNC="sync"
  ASYNC="async"
  AWAIT="await"
  YIELD="yield"

  LBRACE="{"
  RBRACE="}"
  LBRACKET="["
  RBRACKET="]"
  LPAREN="("
  RPAREN=")"
  SEMICOLON=";"
  MINUS="-"
  MINUS_EQ="-="
  MINUS_MINUS="--"
  PLUS="+"
  PLUS_PLUS="++"
  PLUS_EQ="+="
  DIV="/"
  DIV_EQ="/="
  MUL="*"
  MUL_EQ="*="
  INT_DIV="~/"
  INT_DIV_EQ="~/="
  REM_EQ="%="
  REM="%"
  BIN_NOT="~"
  NOT="!"
  
  EXPRESSION_BODY_DEF="=>"
  EQ="="
  EQ_EQ="=="
  NEQ="!="
  DOT="."
  COMMA=","
  COLON=":"
  GT=">"
  GT_EQ=">="
  GT_GT_EQ=">>="
  LT="<"
  LT_EQ="<="
  LT_LT="<<"
  LT_LT_EQ="<<="
  QUEST="?"
  OR="|"
  OR_EQ="|="
  OR_OR="||"
  XOR="^"
  XOR_EQ="^="
  AND="&"
  AND_EQ="&="
  AND_AND="&&"
  HASH="#"
  AT="@"
  ]

  extends(".*Expression")="expression"
}

dartUnit ::= topLevelDefinition*
private topLevelDefinition ::= libraryStatement
                             | partOfStatement
                             | importStatement
                             | exportStatement
                             | partStatement
                             | classDefinition
                             | enumDefinition
                             | functionTypeAlias
                             | getterOrSetterDeclaration
                             | functionDeclarationWithBodyOrNative
                             | varDeclarationListWithSemicolon
                             | incompleteDeclaration // not valid according to spec, but we'd like it to be parsed in order to have completion
                             {recoverWhile="top_level_recover"}
private top_level_recover ::= !(<<nonStrictID>> | '@' | 'abstract' | 'class' | 'const' | 'enum' | 'export' | 'external' | 'final' | 'get' | 'import' | 'library' | 'part' | 'set' | 'static' | 'typedef' | 'var' | 'void')

incompleteDeclaration ::= metadata* ('external' | 'static' | 'final' | 'const')* type | metadata+;

partOfStatement ::= metadata* 'part' 'of' libraryId ';' {
  pin=4
  methods = [ getLibraryName ]
}

libraryId ::= <<nonStrictID>> ('.' <<nonStrictID>>)*
{mixin="com.jetbrains.lang.dart.psi.impl.DartLibraryIdBase" implements="com.jetbrains.lang.dart.psi.DartReference"}

libraryStatement ::= metadata* 'library' libraryNameElement ';' {pin=3}

libraryNameElement ::= <<nonStrictID>> ('.' <<nonStrictID>>)* {
  mixin="com.jetbrains.lang.dart.psi.impl.DartLibraryNameElementBase"
  implements="com.jetbrains.lang.dart.psi.DartPsiCompositeElement, com.intellij.psi.PsiNameIdentifierOwner"
  methods=[getName]
}

importStatement ::= metadata* 'import' uriElement ('deferred'? 'as' componentName )? combinator* ';' {
  pin=3
  implements="com.jetbrains.lang.dart.psi.DartImportOrExportStatement"
  methods = [
    getUriString
    getUriStringOffset
    importPrefix = "componentName"
  ]
}

exportStatement ::= metadata* 'export' uriElement combinator* ';' {
  pin=3
  implements="com.jetbrains.lang.dart.psi.DartImportOrExportStatement"
  methods = [
    getUriString
    getUriStringOffset
  ]
}

private combinator ::= showCombinator | hideCombinator

showCombinator ::= 'show' libraryReferenceList {pin=1}
hideCombinator ::= 'hide' libraryReferenceList {pin=1}
libraryReferenceList ::= libraryComponentReferenceExpression (',' libraryComponentReferenceExpression)*

partStatement ::= metadata* 'part' uriElement ';' {
  pin=3
  implements="com.jetbrains.lang.dart.psi.DartUriBasedDirective"
  methods = [
    getUriString
    getUriStringOffset
  ]
}

metadata ::= '@' simpleQualifiedReferenceExpression arguments?

uriElement ::= stringLiteralExpression {mixin="com.jetbrains.lang.dart.psi.impl.DartUriElementBase"}

varAccessDeclaration ::= metadata* 'static'? (finalOrConst type componentName | finalOrConst componentName | type componentName | 'var' componentName)
{mixin="com.jetbrains.lang.dart.psi.impl.AbstractDartComponentImpl" implements="com.jetbrains.lang.dart.psi.DartComponent"}
private finalOrConst ::= 'final' | 'const'
private varDeclarationListWithSemicolon ::= varDeclarationList ';'{pin=1}
varDeclarationList ::= varAccessDeclaration varInit? (',' varDeclarationListPart)* {pin = 3}
varDeclarationListPart ::= componentName varInit?
{
  mixin="com.jetbrains.lang.dart.psi.impl.AbstractDartComponentImpl"
  implements="com.jetbrains.lang.dart.psi.DartComponent"
  methods= [getMetadataList]
}

operatorDeclaration ::= operatorDeclarationWithReturnType | operatorDeclarationWithoutReturnType // 'operator' is a valid identifier, so can be a returnType
{mixin="com.jetbrains.lang.dart.psi.impl.AbstractDartOperator" implements="com.jetbrains.lang.dart.psi.DartOperator"}
private operatorDeclarationWithReturnType    ::= metadata* 'external'? returnType 'operator' userDefinableOperator formalParameterList (';' | functionBodyOrNative) {pin=5}
private operatorDeclarationWithoutReturnType ::= metadata* 'external'?            'operator' userDefinableOperator formalParameterList (';' | functionBodyOrNative) {pin=4}

classDefinition ::= metadata* 'abstract'? 'class' componentName typeParameters? (mixinApplication | standardClassDeclarationTail)
{pin=4 mixin="com.jetbrains.lang.dart.psi.impl.AbstractDartPsiClass" implements="com.jetbrains.lang.dart.psi.DartClass"}

enumDefinition ::= metadata* 'enum' componentName '{' enumConstantDeclaration (',' enumConstantDeclaration)* ','? '}'
{pin=3  mixin="com.jetbrains.lang.dart.psi.impl.AbstractDartPsiClass" implements="com.jetbrains.lang.dart.psi.DartClass"}

enumConstantDeclaration ::= componentName
{
  mixin="com.jetbrains.lang.dart.psi.impl.AbstractDartComponentImpl"
  implements="com.jetbrains.lang.dart.psi.DartComponent"
  methods = [getMetadataList]
}

mixinApplication ::= '=' type mixins? interfaces? ';' {pin = 1}
private standardClassDeclarationTail ::= (superclass mixins?)? interfaces? ('native' stringLiteralExpression?)? classBody?

classBody ::= '{' classMembers '}' {pin=1}
classMembers ::= classMemberDefinition*
{recoverWhile="simple_scope_recover" implements="com.jetbrains.lang.dart.psi.DartExecutionScope"}
private simple_scope_recover ::= !'}'

private getterOrSetterDeclaration ::= getterDeclaration | setterDeclaration

getterDeclaration ::= getterDeclarationWithReturnType | getterDeclarationWithoutReturnType // 'get' is a valid identifier, so can be a returnType
{mixin="com.jetbrains.lang.dart.psi.impl.AbstractDartComponentImpl" implements="com.jetbrains.lang.dart.psi.DartComponent"}
private getterDeclarationWithReturnType    ::= metadata* ('external' | 'static')* returnType 'get' componentName formalParameterList? (';' | functionBodyOrNative) {pin=5}
private getterDeclarationWithoutReturnType ::= metadata* ('external' | 'static')*            'get' componentName formalParameterList? (';' | functionBodyOrNative) {pin=4}

setterDeclaration ::= setterDeclarationWithReturnType | setterDeclarationWithoutReturnType // 'set' is a valid identifier, so can be a returnType
{mixin="com.jetbrains.lang.dart.psi.impl.AbstractDartComponentImpl" implements="com.jetbrains.lang.dart.psi.DartComponent"}
private setterDeclarationWithReturnType    ::= metadata* ('external' | 'static')* returnType 'set' componentName formalParameterList (';' | functionBodyOrNative) {pin=5}
private setterDeclarationWithoutReturnType ::= metadata* ('external' | 'static')*            'set' componentName formalParameterList (';' | functionBodyOrNative) {pin=4}

typeParameters ::= '<' typeParameter (',' typeParameter)* '>' {pin=1}
typeParameter ::= metadata* componentName ('extends' type)?
{recoverWhile="type_parameter_recover" mixin="com.jetbrains.lang.dart.psi.impl.AbstractDartComponentImpl" implements="com.jetbrains.lang.dart.psi.DartComponent"}

private type_parameter_recover ::= !(<<nonStrictID>> | '(' | ',' | '=' | '>' | '@' | 'abstract' | 'class' | 'const' | 'export' | 'extends' |
                                     'external' | 'final' | 'get' | 'implements' | 'import' | 'library' | 'native' | 'part' | 'set' |
                                     'static' | 'typedef' | 'var' | 'void' | '{')
superclass ::= 'extends' type {pin=1}
mixins ::= 'with' typeList {pin=1}
interfaces ::= 'implements' typeList {pin=1}

private classMemberDefinition ::= factoryConstructorDeclaration
                                | namedConstructorDeclaration
                                | operatorDeclaration
                                | getterOrSetterDeclaration
                                | methodDeclaration
                                | varDeclarationListWithSemicolon
                                | incompleteDeclaration // not valid according to spec, but we'd like it to be parsed in order to have completion
                                {recoverWhile="class_member_recover"}
private class_member_recover ::= !(<<nonStrictID>> | '@' | 'abstract' | 'class' | 'const' | 'export' | 'external' | 'factory' | 'final' | 'get' | 'import' | 'library' | 'operator' | 'part' | 'set' | 'static' | 'typedef' | 'var' | 'void' | '}' )

private functionBodyOrNative ::= 'native' functionBody
                               | functionNative
                               | functionBody

private functionNative ::= 'native' (stringLiteralExpression ';' | ';' | stringLiteralExpression functionBody)

methodDeclaration ::= metadata* ('external' | 'static' | 'const')* functionDeclarationPrivate initializers? (';' | functionBodyOrNative | redirection)?
{pin=3 mixin="com.jetbrains.lang.dart.psi.impl.AbstractDartComponentImpl" implements="com.jetbrains.lang.dart.psi.DartComponent"}
namedConstructorDeclaration ::= metadata* ('external' | 'const')* referenceExpression '.' componentName formalParameterList initializers? (';' | functionBodyOrNative | redirection)?
{pin=6 mixin="com.jetbrains.lang.dart.psi.impl.AbstractDartComponentImpl" implements="com.jetbrains.lang.dart.psi.DartComponent"}

initializers ::= ':' superCallOrFieldInitializer (',' superCallOrFieldInitializer)*

redirection ::= ':' 'this' ('.' referenceExpression)? arguments  {pin=2}

fieldInitializer ::= ('this' '.')? referenceExpression '=' expression {pin=2}

superCallOrFieldInitializer ::= ('super' | 'this') ('.' referenceExpression)? arguments
                              | fieldInitializer
                              {recoverWhile="super_call_or_field_initializer_recover"}
private super_call_or_field_initializer_recover ::= !(<<nonStrictID>> | ',' | ':' | ';' | '=>' | '@' | 'abstract' | 'async' | 'class' | 'const' |
                                                      'export' | 'external' | 'factory' | 'final' | 'get' | 'import' | 'library' |
                                                      'native' | 'operator' | 'part' | 'set' | 'static' | 'sync' | 'typedef' | 'var' | 'void' | '{' |
                                                      '}' )

functionTypeAlias ::= metadata* 'typedef' functionPrefix typeParameters? formalParameterList ';'
{pin=5 mixin="com.jetbrains.lang.dart.psi.impl.AbstractDartComponentImpl" implements="com.jetbrains.lang.dart.psi.DartComponent"}
private functionPrefix ::= returnType componentName | componentName

factoryConstructorDeclaration ::= metadata* ('external' | 'const')* 'factory' referenceExpression ('.' componentName)? formalParameterList factoryTail?
{pin=3 mixin="com.jetbrains.lang.dart.psi.impl.AbstractDartComponentImpl" implements="com.jetbrains.lang.dart.psi.DartComponent"}
private factoryTail ::= varFactoryDeclaration ';' | functionBodyOrNative | ';' {pin(".*")=1}
private varFactoryDeclaration ::= '=' type ['.' referenceExpression] {pin(".*")=1}

userDefinableOperator ::= binaryOperator |
                          '~' |
                          '[' ']' '='?
private binaryOperator ::= multiplicativeOperator |
                           additiveOperator |
                           shiftOperator|
                           relationalOperator|
                           '==' |
                           bitwiseOperator

multiplicativeOperator ::= '*' | '/' | '%' | '~/'
additiveOperator ::= '+' | '-'
shiftOperator ::= '<<' | shiftRightOperator
relationalOperator ::= '>=' | '>' | '<=' | '<'
bitwiseOperator ::= '&' | '^' | '|'
shiftRightOperator ::= '>' '>'

assignmentOperator ::= '=' | '*=' | '/=' | '~/=' | '%=' | '+=' | '-=' | '<<=' | '>>=' | '&=' | '^=' | '|='
equalityOperator ::= '==' | '!='

formalParameterList ::= '(' normalFormalParameter (',' normalFormalParameter)* (',' namedFormalParameters)? ')'
                      | '(' namedFormalParameters? ')'

fieldFormalParameter ::= metadata* finalConstVarOrType? 'this' '.' referenceExpression formalParameterList?

namedFormalParameters ::= '[' defaultFormalNamedParameter (',' defaultFormalNamedParameter)* ']' |
                          '{' defaultFormalNamedParameter (',' defaultFormalNamedParameter)* '}'

defaultFormalNamedParameter ::= normalFormalParameter (('=' | ':') expression)? {recoverWhile="default_formal_parameter_recover"}
private default_formal_parameter_recover ::= !(')' | ',' | ']' | '}')

normalFormalParameter ::= functionSignature
                        | fieldFormalParameter
                        | simpleFormalParameter
                        | incorrectNormalFormalParameter {
  methods = [findComponentName]
}
private incorrectNormalFormalParameter ::= '<' + <<nonStrictID>> + '>' <<nonStrictID>> // because Analysis Server gives syntactically incorrect quick fixes, e.g. "void foo(<bottom> arg0) {\n}"

simpleFormalParameter ::= declaredIdentifier | metadata* componentName
{mixin="com.jetbrains.lang.dart.psi.impl.AbstractDartComponentImpl" implements="com.jetbrains.lang.dart.psi.DartComponent"}
private declaredIdentifier ::= metadata* finalConstVarOrTypeAndComponentName;

returnType ::= 'void' | type

private finalConstVarOrType ::= 'final' type? | 'const' type? | 'var' | type // Be careful on using it! It may erroneously consume 'type?'. See finalConstVarOrTypeAndComponentName
private finalConstVarOrTypeAndComponentName ::= 'final' type componentName |
                                                'final'      componentName |
                                                'const' type componentName |
                                                'const'      componentName |
                                                'var'        componentName |
                                                        type componentName

componentName ::= << nonStrictID >>
{mixin="com.jetbrains.lang.dart.psi.impl.DartNamedElementImpl" implements="com.jetbrains.lang.dart.psi.DartNamedElement"}

// see nonStrictID
id ::= IDENTIFIER

type ::= simpleQualifiedReferenceExpression typeArguments? {
  pin=1
  methods=[
    resolveReference
  ]
}

typeArguments ::= '<' typeList '>'
typeList ::= type (',' type)*

block ::= '{' statements '}' {pin=1}

statements ::= statement* {implements="com.jetbrains.lang.dart.psi.DartExecutionScope"}
private statement ::= label* nonLabelledStatement

private nonLabelledStatement ::= block // Guard to break tie with map literal.  todo why ';'?
                               | functionDeclarationWithBody ';'?
                               | forStatement ';'?
                               | whileStatement ';'?
                               | doWhileStatement ';'?
                               | switchStatement ';'?
                               | ifStatement ';'?
                               | rethrowStatement
                               | tryStatement
                               | breakStatement
                               | continueStatement
                               | returnStatement
                               | throwStatementWithSemicolon
                               | assertStatement
                               | statementFollowedBySemiColon
                               | yieldEachStatement
                               | yieldStatement
                               | ';'
private statementFollowedBySemiColon ::= (varDeclarationList | expression) ';' {pin=1}

label::= componentName ':'
{
  mixin="com.jetbrains.lang.dart.psi.impl.AbstractDartComponentImpl"
  implements="com.jetbrains.lang.dart.psi.DartComponent"
  methods = [getMetadataList]
}

private expressionWithRecoverUntilParen ::= expression {recoverWhile="not_paren_recover"}
private not_paren_recover ::= !')'

whileStatement ::= 'while' '(' expressionWithRecoverUntilParen ')' statement {pin=1}
doWhileStatement ::= 'do' statement 'while' '(' expressionWithRecoverUntilParen ')' ';' {pin=1}
forStatement ::= 'await'? 'for' forLoopPartsInBraces statement {pin=2}
//todo component
assertStatement ::= 'assert' '(' expressionWithRecoverUntilParen ')' ';' {pin=1}
yieldEachStatement ::= 'yield' '*' expression ';' {pin=2}
yieldStatement ::= 'yield' expression ';' {pin=1}
breakStatement ::= 'break' referenceExpression? ';' {pin=1}
continueStatement ::= 'continue' referenceExpression? ';' {pin=1}
returnStatement ::= 'return' expression? ';' {pin=1}
rethrowStatement ::= 'rethrow' ';' {pin=1}

forLoopPartsInBraces::= '(' forLoopParts ')'
forLoopParts::= varDeclarationList ';' expression? ';' expressionList?
              | forInPart
              | expressionList? ';' expression? ';' expressionList?
              {recoverWhile="for_loops_parts_recover"}

forInPart ::= (varAccessDeclaration | componentName) 'in' expression

private for_loops_parts_recover ::= !')'

ifStatement ::= 'if' '(' expressionWithRecoverUntilParen ')' statement ('else' statement)?  {pin=1}
switchStatement ::= 'switch' '(' expressionWithRecoverUntilParen ')' '{' switchCase* defaultCase? '}' {pin=1}

switchCase ::= label* 'case' expression ':' statements {pin=2}
defaultCase::= label* 'default' ':' statements {pin = 2}

tryStatement ::= 'try' block (onPart+ finallyPart? | finallyPart) {pin=1}
onPart ::= catchPart block | 'on' type catchPart? block {pin=1}
catchPart ::= 'catch' '(' componentName (',' componentName)? ')' {pin=1}
finallyPart::= 'finally' block {pin=1}

varInit ::= '=' expression {pin=1}

expressionList ::= expression (',' expression)*
arguments ::= '(' argumentList? ')'

argumentList ::= argumentListPart (',' argumentListPart)* {recoverWhile="argument_list_recover"}
private argument_list_recover ::= !(')')
private argumentListPart ::= namedArgument | expression {recoverWhile="argument_list_part_recover"}
private argument_list_part_recover ::= !(')' | ',')

namedArgument ::= parameterNameReferenceExpression ':' expression {
  methods = [
    getParameterReferenceExpression
    getExpression
  ]
}

parameterNameReferenceExpression ::= << nonStrictID >>
{mixin="com.jetbrains.lang.dart.psi.impl.DartReferenceImpl" implements="com.jetbrains.lang.dart.psi.DartReference"}

expression ::= assignExpressionWrapper {recoverWhile="expression_recover"}
private expression_recover ::= !(<<nonStrictID>> | <<parenthesizedExpressionWrapper>> | '!' | '!=' | '%' | '%=' |
                                 '&&' | '&' | '&=' | '(' | ')' | '*' | '*=' | '+' | '++' | '+=' | ',' | '-' | '--' | '-=' | '.' | '/' |
                                 '/=' | ':' | ';' | '<' | '<<' | '<<=' | '<=' | '=' | '==' | '=>' | '>' | '>=' | '>>=' | '?' | '@' | '[' |
                                 ']' | '^' | '^=' | 'abstract' | 'as' | 'assert' | 'async' | 'break' | 'case' | 'catch' | 'class' | 'const' |
                                 'continue' | 'default' | 'deferred' | 'do' | 'else' | 'export' | 'external' | 'factory' | 'final' | 'finally' | 'for' |
                                 'get' | 'hide' | 'if' | 'import' | 'is' | 'library' | 'native' | 'new' | 'on' | 'operator' | 'part' |
                                 'rethrow' | 'return' | 'set' | 'show' | 'static' | 'super' | 'switch' | 'sync' | 'this' | 'throw' | 'try' |
                                 'typedef' | 'var' | 'void' | 'while' | '{' | '|' | '|=' | '||' | '}' | '~' | '~/' | '~/=' | CLOSING_QUOTE |
                                  FALSE | HASH | HEX_NUMBER | LONG_TEMPLATE_ENTRY_END | LONG_TEMPLATE_ENTRY_START | NULL | NUMBER |
                                  OPEN_QUOTE | RAW_SINGLE_QUOTED_STRING | RAW_TRIPLE_QUOTED_STRING | REGULAR_STRING_PART |
                                  SHORT_TEMPLATE_ENTRY_START | TRUE)

private assignExpressionWrapper ::= ternaryExpressionWrapper assignExpression*
left assignExpression ::= assignmentOperator ternaryExpressionWrapper {pin=1} // todo right associate

private ternaryExpressionWrapper ::= logicOrExpressionWrapper ternaryExpression?
left ternaryExpression ::= '?' expression ':' ternaryExpressionWrapper

private logicOrExpressionWrapper ::= logicAndExpressionWrapper logicOrExpression*
left logicOrExpression ::= '||' logicAndExpressionWrapper
{mixin="com.jetbrains.lang.dart.psi.impl.DartOperatorExpressionImpl" implements="com.jetbrains.lang.dart.psi.DartReference"}

private logicAndExpressionWrapper ::= compareExpressionWrapper logicAndExpression*
left logicAndExpression ::= '&&' compareExpressionWrapper
{mixin="com.jetbrains.lang.dart.psi.impl.DartOperatorExpressionImpl" implements="com.jetbrains.lang.dart.psi.DartReference"}

private compareExpressionWrapper ::= bitwiseExpressionWrapper compareExpression*
left compareExpression ::= (relationalOperator | equalityOperator) bitwiseExpressionWrapper
{mixin="com.jetbrains.lang.dart.psi.impl.DartOperatorExpressionImpl" implements="com.jetbrains.lang.dart.psi.DartReference"}

private bitwiseExpressionWrapper ::= shiftExpressionWrapper bitwiseExpression*
left bitwiseExpression ::= bitwiseOperator shiftExpressionWrapper
{mixin="com.jetbrains.lang.dart.psi.impl.DartOperatorExpressionImpl" implements="com.jetbrains.lang.dart.psi.DartReference"}

private shiftExpressionWrapper ::= additiveExpressionWrapper shiftExpression*
left shiftExpression ::= shiftOperator additiveExpressionWrapper
{mixin="com.jetbrains.lang.dart.psi.impl.DartOperatorExpressionImpl" implements="com.jetbrains.lang.dart.psi.DartReference"}

private additiveExpressionWrapper ::= multiplicativeExpressionWrapper additiveExpression*
left additiveExpression ::= additiveOperator multiplicativeExpressionWrapper
{mixin="com.jetbrains.lang.dart.psi.impl.DartOperatorExpressionImpl" implements="com.jetbrains.lang.dart.psi.DartReference"}

private multiplicativeExpressionWrapper ::= prefixExpression multiplicativeExpression*
left multiplicativeExpression ::= multiplicativeOperator prefixExpression
{mixin="com.jetbrains.lang.dart.psi.impl.DartOperatorExpressionImpl" implements="com.jetbrains.lang.dart.psi.DartReference"}

prefixExpression ::= (prefixOperator prefixExpression) | awaitExpression | suffixExpressionWrapper
{mixin="com.jetbrains.lang.dart.psi.impl.DartOperatorExpressionImpl" implements="com.jetbrains.lang.dart.psi.DartReference"}
prefixOperator ::= '-' | '+' | '--' | '++' | '!' | '~' | '?'

awaitExpression ::= 'await' prefixExpression

private suffixExpressionWrapper ::= valueExpression suffixExpression*
left suffixExpression ::= ('--' | '++')
{mixin="com.jetbrains.lang.dart.psi.impl.DartOperatorExpressionImpl" implements="com.jetbrains.lang.dart.psi.DartReference"}

valueExpression ::= primary callOrArrayAccessOrQualifiedRefExpression (isExpression | asExpression)? cascadeReferenceExpression*
left isExpression ::= 'is' '!'? type
left asExpression ::= 'as' type
{mixin="com.jetbrains.lang.dart.psi.impl.DartClassReferenceImpl" implements="com.jetbrains.lang.dart.psi.DartReference"}

private primary ::=  literalExpression |
                     functionExpression |
                     newExpression |
                     constConstructorExpression |
                     refOrThisOrSuperOrParenExpression |
                     throwExpression

parenthesizedExpression ::= '(' expressionInParentheses ')'
{pin=1 mixin="com.jetbrains.lang.dart.psi.impl.DartClassReferenceImpl" implements="com.jetbrains.lang.dart.psi.DartReference"}
private expressionInParentheses ::= expression | statement {recoverWhile=parenthesesRecovery}
private parenthesesRecovery ::= !')'

private callOrArrayAccessOrQualifiedRefExpression ::= (callExpression | arrayAccessExpression | qualifiedReferenceExpression)*

private refOrThisOrSuperOrParenExpression ::= (referenceExpression | thisExpression | superExpression | << parenthesizedExpressionWrapper >>)

left callExpression ::= arguments
{mixin="com.jetbrains.lang.dart.psi.impl.DartReferenceImpl" implements="com.jetbrains.lang.dart.psi.DartReference"}
left arrayAccessExpression ::= arrayAccess
{mixin="com.jetbrains.lang.dart.psi.impl.DartClassReferenceImpl" implements="com.jetbrains.lang.dart.psi.DartReference"}

private arrayAccess ::= '[' expression? ']' {pin=1}

libraryComponentReferenceExpression ::= << nonStrictID >>
{mixin="com.jetbrains.lang.dart.psi.impl.DartLibraryComponentReferenceExpressionBase" implements="com.jetbrains.lang.dart.psi.DartReference"}

referenceExpression ::= << nonStrictID >>
{mixin="com.jetbrains.lang.dart.psi.impl.DartReferenceImpl" implements="com.jetbrains.lang.dart.psi.DartReference"}

left qualifiedReferenceExpression ::= '.' referenceExpression {elementType="referenceExpression"}
left cascadeReferenceExpression ::= '.''.' << cascadeStopper >> (arrayAccess | refOrThisOrSuperOrParenExpression callOrArrayAccessOrQualifiedRefExpression) << varInitWrapper >>
{mixin="com.jetbrains.lang.dart.psi.impl.DartReferenceImpl" implements="com.jetbrains.lang.dart.psi.DartReference"}

simpleQualifiedReferenceExpression ::= referenceExpression qualifiedReferenceExpression* {elementType="referenceExpression"}

thisExpression ::= 'this'
{mixin="com.jetbrains.lang.dart.psi.impl.DartReferenceImpl" implements="com.jetbrains.lang.dart.psi.DartReference"}
superExpression ::= 'super'
{mixin="com.jetbrains.lang.dart.psi.impl.DartReferenceImpl" implements="com.jetbrains.lang.dart.psi.DartReference"}

newExpression ::= 'new' type ('.' referenceExpression)? arguments
{pin=1 mixin="com.jetbrains.lang.dart.psi.impl.DartReferenceImpl" implements="com.jetbrains.lang.dart.psi.DartReference"}
constConstructorExpression ::= 'const' type ('.' referenceExpression)? arguments
{pin=1 mixin="com.jetbrains.lang.dart.psi.impl.DartReferenceImpl" implements="com.jetbrains.lang.dart.psi.DartReference"}

throwExpression ::= 'throw' expression

// todo get rid of all Wrappers and WITHOUT_CASCADE user data, implement expression parsing according to the spec and Grammar-Kit docs
literalExpression ::= NULL | TRUE | FALSE | NUMBER | HEX_NUMBER | stringLiteralExpression | symbolLiteralExpression | <<mapLiteralExpressionWrapper>> | <<listLiteralExpressionWrapper>>
{mixin="com.jetbrains.lang.dart.psi.impl.DartClassReferenceImpl" implements="com.jetbrains.lang.dart.psi.DartReference"}

symbolLiteralExpression ::= HASH (userDefinableOperator | simpleQualifiedReferenceExpression) {pin=1}

stringLiteralExpression ::= (RAW_SINGLE_QUOTED_STRING | RAW_TRIPLE_QUOTED_STRING | stringTemplate)+
{mixin="com.jetbrains.lang.dart.psi.impl.DartClassReferenceImpl" implements="com.jetbrains.lang.dart.psi.DartReference"}

private stringTemplate ::= OPEN_QUOTE (REGULAR_STRING_PART | shortTemplateEntry | longTemplateEntry)* CLOSING_QUOTE {pin=1}
shortTemplateEntry ::= SHORT_TEMPLATE_ENTRY_START (thisExpression | referenceExpression) {pin=1}
longTemplateEntry ::= LONG_TEMPLATE_ENTRY_START expression LONG_TEMPLATE_ENTRY_END {pin=1}

listLiteralExpression ::= 'const'? typeArguments? '[' (expressionList ','?)? ']'
{mixin="com.jetbrains.lang.dart.psi.impl.DartClassReferenceImpl" implements="com.jetbrains.lang.dart.psi.DartReference"}
mapLiteralExpression ::= 'const'? typeArguments? '{' (mapLiteralEntry (',' mapLiteralEntry)* ','? )? '}'
{mixin="com.jetbrains.lang.dart.psi.impl.DartClassReferenceImpl" implements="com.jetbrains.lang.dart.psi.DartReference"}

mapLiteralEntry ::= expression ':' expression {recoverWhile="map_literal_entry_recover"}
private map_literal_entry_recover ::= !(',' | '}')

functionExpression ::= formalParameterList functionExpressionBody
functionSignature ::= metadata* functionDeclarationPrivate
{mixin="com.jetbrains.lang.dart.psi.impl.AbstractDartComponentImpl" implements="com.jetbrains.lang.dart.psi.DartComponent"}
private functionDeclarationPrivate ::= returnType componentName formalParameterList | componentName formalParameterList // todo remove, use functionSignature as in spec
functionDeclarationWithBodyOrNative ::= metadata* 'external'? (returnType componentName formalParameterList | componentName formalParameterList) ( ';' | functionBodyOrNative)
{mixin="com.jetbrains.lang.dart.psi.impl.AbstractDartComponentImpl" implements="com.jetbrains.lang.dart.psi.DartComponent"}
functionDeclarationWithBody ::= metadata*  (returnType componentName formalParameterList | componentName formalParameterList) functionBody // todo merge with functionDeclarationWithBodyOrNative
{mixin="com.jetbrains.lang.dart.psi.impl.AbstractDartComponentImpl" implements="com.jetbrains.lang.dart.psi.DartComponent"}

functionBody ::= arrowBodyWithSemi | <<blockBodyWrapper>> {methods=[getBlock]}
private arrowBodyWithSemi ::= <<arrowBodyWrapper>> ';' {pin=1}
functionExpressionBody ::= <<arrowBodyWrapper>> | <<blockBodyWrapper>>

//functionBody ::= arrowBodyWithSemi | blockBody {methods=[getBlock]}
//private arrowBodyWithSemi ::= arrowBody ';' {pin=1}
//functionExpressionBody ::= arrowBody | blockBody

private arrowBody ::= ('async' '*'? | 'sync' '*'?)? '=>' expression {pin=2}
private blockBody ::= ('async' '*'? | 'sync' '*'?)? block

