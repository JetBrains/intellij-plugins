plugins {
    id "org.jetbrains.intellij" version "0.2.15"
    id "org.jetbrains.kotlin.jvm" version "1.1.4-3"
}

intellij {
    version idea_version
    pluginName 'CloudFormation'
    plugins 'yaml'
    updateSinceUntilBuild false
}

allprojects {
    apply plugin: 'kotlin'

    repositories {
        mavenCentral()
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    dependencies {
        compile 'com.thoughtworks.xstream:xstream:1.4.10'
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

project(':metadata-crawler') {
    apply plugin: 'org.jetbrains.intellij'

    intellij {
        version idea_version
        plugins 'yaml'
    }

    dependencies {
        compile rootProject

        compile 'commons-io:commons-io:2.4'
        compile 'org.apache.commons:commons-lang3:3.3.2'
        compile 'net.htmlparser.jericho:jericho-html:3.3'
        compile 'org.jsoup:jsoup:1.8.1'
        compile 'org.apache.directory.studio:org.apache.commons.codec:1.8'
    }

    task collectMetadata(type: JavaExec, dependsOn: classes) {
        main = 'MetadataCrawlerMain'
        workingDir = rootDir
        systemProperty "file.encoding", "UTF-8"
        classpath sourceSets.main.runtimeClasspath
    }
}

task printIdeaVersionToTeamCity(type: JavaExec, dependsOn: testClasses) {
    main = 'com.intellij.aws.cloudformation.tests.PrintIdeaVersionToTeamCity'
    workingDir = rootDir
    systemProperty "file.encoding", "UTF-8"
    classpath sourceSets.test.runtimeClasspath
}

task printVersion {
    println 'version = ' + version
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}
