buildscript {
    ext.kotlin_version = "1.2.71"
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.viswaramamoorthy:gradle-util-plugins:0.1.0-RELEASE"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.3.11"
}

// Workaround too long command line for JavaExec
// https://github.com/viswaramamoorthy/gradle-util-plugins
// https://github.com/gradle/gradle/issues/1989
apply plugin: 'ManifestClasspath'

intellij {
    version idea_version
    pluginName 'CloudFormation'
    plugins 'yaml'
    updateSinceUntilBuild false
}

allprojects {
    apply plugin: 'kotlin'

    repositories {
        mavenCentral()
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        compile 'com.thoughtworks.xstream:xstream:1.4.10'
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

project(':metadata-crawler') {
    dependencies {
        compile rootProject

        compile 'commons-io:commons-io:2.4'
        compile 'org.apache.commons:commons-lang3:3.3.2'
        compile 'net.htmlparser.jericho:jericho-html:3.3'
        compile 'org.jsoup:jsoup:1.8.1'
        compile 'org.apache.directory.studio:org.apache.commons.codec:1.8'
    }

    task collectMetadata(type: JavaExec, dependsOn: classes) {
        main = 'MetadataCrawlerMain'
        workingDir = rootDir
        systemProperty "file.encoding", "UTF-8"
        classpath sourceSets.main.runtimeClasspath
    }
}

task printIdeaVersionToTeamCity(type: JavaExec, dependsOn: testClasses) {
    main = 'com.intellij.aws.cloudformation.tests.PrintIdeaVersionToTeamCity'
    workingDir = rootDir
    systemProperty "file.encoding", "UTF-8"
    classpath sourceSets.test.runtimeClasspath
}

task printVersion {
    println 'version = ' + version
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10.2'
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}
